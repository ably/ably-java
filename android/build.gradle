buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
    }
}

apply plugin: 'com.android.library'
apply from: '../common.gradle'

ext {
    artifactId = 'ably-android'
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    compileSdkVersion 22
    buildToolsVersion '27.0.3'

    dexOptions {
     // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
     preDexLibraries = preDexEnabled && !travisBuild
   }

    defaultConfig {
        buildConfigField 'String', 'LIBRARY_NAME', '"android"'
        buildConfigField 'String', 'VERSION', "\"$version\""
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName version
        setProperty("archivesBaseName", "ably-android-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "class", "io.ably.lib.test.rest.RestSuite,io.ably.lib.test.realtime.RealtimeSuite,io.ably.lib.test.android.AndroidSuite"
        testInstrumentationRunnerArgument "timeout_msec", "150000"
        consumerProguardFiles 'proguard.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', '../lib/src/main/java']
           }
        }
        androidTest {
            java {
                srcDirs = ['src/androidTest/java', '../lib/src/test/java']
            }
            assets {
                srcDirs = ['../lib/src/test/resources']
            }
        }
    }
}

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
    task.doFirst {
        logging.level = LogLevel.INFO
    }
    task.doLast {
        logging.level = LogLevel.LIFECYCLE
    }
}

apply from: '../dependencies.gradle'
dependencies {
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"
}

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
    androidTestCompile {
        extendsFrom testCompile
    }
}

apply from: 'maven.gradle'

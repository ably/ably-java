apply plugin: 'com.android.library'

ext {
    artifactId = 'ably-android'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        buildConfigField 'String', 'LIBRARY_NAME', '"android"'
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task copyResDirectoryToClasses(type: Copy){
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
    into "${buildDir}/intermediates/classes/test/release/resources"
}

tasks.assemble.dependsOn(tasks.copyResDirectoryToClasses)

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
    task.doFirst {
        logging.level = LogLevel.INFO
    }
    task.doLast {
        logging.level = LogLevel.LIFECYCLE
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":core")

    androidTestCompile 'com.android.support.test:runner:0.5'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'com.nanohttpd:nanohttpd:2.2.0'
    testCompile 'org.nanohttpd:nanohttpd-nanolets:2.3.0'
    testCompile 'org.mockito:mockito-core:2.0.46-beta'
    testCompile project(path: ':core', configuration: 'coreTestsConfiguration')
}

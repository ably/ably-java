apply plugin: 'com.android.library'

ext {
    artifactId = 'ably-android'
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        buildConfigField 'String', 'LIBRARY_NAME', '"android"'
        buildConfigField 'String', 'VERSION', "\"$version\""
        minSdkVersion 8
        targetSdkVersion 24
        versionCode 1
        versionName version
        setProperty("archivesBaseName", "ably-android-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "class", "io.ably.lib.test.rest.RestSuite,io.ably.lib.test.realtime.RealtimeSuite"
        testInstrumentationRunnerArgument "timeout_msec", "150000"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDirs = ['../lib/src/main/java']
           }
        }
        androidTest {
            java {
                srcDirs = ['src/androidTest/java', '../lib/src/test/java']
            }
            assets {
                srcDirs = ['../lib/src/test/resources']
            }
        }
    }
}

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
    task.doFirst {
        logging.level = LogLevel.INFO
    }
    task.doLast {
        logging.level = LogLevel.LIFECYCLE
    }
}

apply from: '../dependencies.gradle'
dependencies {
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"
}

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
    androidTestCompile {
        extendsFrom testCompile
    }
}

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}

apply from: 'maven.gradle'

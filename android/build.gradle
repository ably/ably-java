plugins {
  id 'com.android.library'
  id 'maven-publish'
  id 'signing'
}

apply from: '../common.gradle'

ext {
  artifactId = 'ably-android'
}

allprojects {
  repositories {
    google()
  }
}


android {
  namespace 'io.ably.lib'

  compileSdkVersion 30

  buildFeatures {
    buildConfig true
  }

  defaultConfig {
    buildConfigField 'String', 'LIBRARY_NAME', '"android"'
    buildConfigField 'String', 'VERSION', "\"$version\""
    minSdkVersion 19
    targetSdkVersion 30
    setProperty('archivesBaseName', "ably-android-$versionName")
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    testInstrumentationRunnerArgument 'class', 'io.ably.lib.test.android.AndroidPushTest'
    testInstrumentationRunnerArgument 'timeout_msec', '300000'
    consumerProguardFiles 'proguard.txt'
  }

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    abortOnError false
  }

  publishing {
    singleVariant("release") {
      withJavadocJar()
      withSourcesJar()
    }
  }
}

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
  task.doFirst {
    logging.level = LogLevel.INFO
  }
  task.doLast {
    logging.level = LogLevel.LIFECYCLE
  }
}

dependencies {
  api project(':lib')
  compileOnly 'com.google.firebase:firebase-messaging:22.0.0'
  androidTestImplementation 'com.android.support.test:runner:0.5'
  androidTestImplementation 'com.android.support.test:rules:0.5'
  androidTestImplementation 'com.crittercism.dexmaker:dexmaker:1.4'
  androidTestImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
  androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
  androidTestImplementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.4'
}

configurations {
  all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
  androidTestImplementation {
    extendsFrom testImplementation
  }
}

apply from: 'maven.gradle'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

apply plugin: 'com.android.library'
apply from: '../common.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    artifactId = 'ably-android'
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '29.0.3'

    defaultConfig {
        buildConfigField 'String', 'LIBRARY_NAME', '"android"'
        buildConfigField 'String', 'VERSION', "\"$version\""
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName version
        setProperty("archivesBaseName", "ably-android-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "class", "io.ably.lib.test.android.AndroidPushTest"
        //testInstrumentationRunnerArgument "class", "io.ably.lib.test.rest.RestSuite,io.ably.lib.test.realtime.RealtimeSuite,io.ably.lib.test.android.AndroidSuite,io.ably.lib.test.android.AndroidPushTest"
        testInstrumentationRunnerArgument "timeout_msec", "300000"
//        testInstrumentationRunnerArgument "ABLY_ENV", "\"$System.env.ABLY_ENV\""
        consumerProguardFiles 'proguard.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', '../lib/src/main/java']
           }
        }
        androidTest {
            java {
                srcDirs = ['src/androidTest/java', '../lib/src/test/java']
            }
            assets {
                srcDirs = ['../lib/src/test/resources']
            }
        }
    }
}

/* Fix for android test logging. Source: https://code.google.com/p/android/issues/detail?id=182307 */
tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
    task.doFirst {
        logging.level = LogLevel.INFO
    }
    task.doLast {
        logging.level = LogLevel.LIFECYCLE
    }
}

apply from: '../dependencies.gradle'
apply from: './dependencies.gradle'
dependencies {
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
}

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
    androidTestImplementation {
        extendsFrom testImplementation
    }
}

apply from: 'maven.gradle'

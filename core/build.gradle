apply plugin: 'java'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    artifactId = 'ably-core'
}

dependencies {
    compile 'org.msgpack:msgpack-core:0.8.2'
    compile 'org.java-websocket:Java-WebSocket:1.3.1'
    compile 'com.google.code.gson:gson:2.5'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'com.nanohttpd:nanohttpd:2.2.0'
    testCompile 'org.nanohttpd:nanohttpd-nanolets:2.3.0'
    testCompile 'org.mockito:mockito-core:2.0.46-beta'
}

task fullJar(type: Jar) {
    baseName = project.name + '-full'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
    exclude 'META-INF/**'
}

task testsJar(type: Jar) {
    baseName = project.name + '-tests'
    from sourceSets.test.output
    classifier = 'test'
    includes = ['**/test/**','**/common/**']
    excludes = ['**/jre/**']
    includeEmptyDirs false
    with jar
}

configurations {
    fullConfiguration
    testsConfiguration
}

artifacts {
    fullConfiguration fullJar
    testsConfiguration testsJar
}

task testRealtimeSuite(type: Test) {
    filter {
        includeTestsMatching "*RealtimeSuite"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    outputs.upToDateWhen { false }
}

task testRestSuite(type: Test) {
    filter {
        includeTestsMatching "*RestSuite"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    outputs.upToDateWhen { false }
}

apply from: '../maven.gradle'
